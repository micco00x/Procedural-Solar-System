<html>
	<head>
		<title>ProceduralSS</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
		
		<script src="third_party/three/three.min.js"></script>
		<script src="third_party/three/controls/FlyControls.js"></script>
		
		<script src="third_party/seedrandom.min.js"></script>
		<script src="third_party/simplex-noise.js"></script>
		<script src="third_party/ImprovedNoise.js"></script>
		
		<script src="third_party/stats/stats.min.js"></script>
		<script src="third_party/gui/dat.gui.min.js"></script>
		
		<script type="x-shader/x-vertex" id="basicVertexShader">
			varying vec3 pos;
			varying vec3 posCameraCoord; // need this becasue light is in camera coords.
			varying vec3 n;
			
			varying vec2 vUV;
			
			void main()
			{
				pos = (modelMatrix * vec4(position, 1.0)).xyz;
				posCameraCoord = (modelViewMatrix * vec4(position, 1.0)).xyz;
				
				n = normalMatrix * normal;
				
				vUV = uv;
				
				gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
			}
		</script>
		<script type="x-shader/x-fragment" id="basicFragmentShader">
			struct PointLight {
				vec3 color;
				vec3 position;
				float distance;
			};
		
			uniform float emissiveLightIntensity;
			uniform PointLight pointLights[NUM_POINT_LIGHTS];
			uniform float pointLightIntensity[NUM_POINT_LIGHTS];
		
			// used to correctly compute noiseHeight when the planet is moved:
			uniform vec3 planetPosition;
			uniform float radius;
			
			uniform sampler2D texture[8];   // textures characterizing the planet
			uniform float textureHeight[8]; // heights from where the texture changes
		
			varying vec3 pos;
			varying vec3 posCameraCoord;
			varying vec3 n;
			
			varying vec2 vUV;
			
			void main()
			{
				// Iterate through lights:
				vec3 totLight = vec3(0.0, 0.0, 0.0);
				for (int i = 0; i < NUM_POINT_LIGHTS; ++i) {
					vec3 l = posCameraCoord - pointLights[i].position;
					if (pointLights[i].distance == 0.0 || length(l) < pointLights[i].distance) {
						vec3 lightDirection = normalize(l);
						totLight += clamp(dot(-lightDirection, n), 0.0, 1.0) * pointLights[i].color * pointLightIntensity[i];
					}
				}
				
				// Compute noise height to determine the texture to be used:
				float noiseHeight = length(pos - planetPosition) - radius;
				
				// Each height has its own texture:
				if (noiseHeight < textureHeight[0]) {
					gl_FragColor = texture2D(texture[0], vUV);
				} else if (noiseHeight < textureHeight[1]) {
					gl_FragColor = texture2D(texture[1], vUV);
				} else if (noiseHeight < textureHeight[2]) {
					gl_FragColor = texture2D(texture[2], vUV);
				} else if (noiseHeight < textureHeight[3]) {
					gl_FragColor = texture2D(texture[3], vUV);
				} else if (noiseHeight < textureHeight[4]) {
					gl_FragColor = texture2D(texture[4], vUV);
				} else if (noiseHeight < textureHeight[5]) {
					gl_FragColor = texture2D(texture[5], vUV);
				} else if (noiseHeight < textureHeight[6]) {
					gl_FragColor = texture2D(texture[6], vUV);
				} else {
					gl_FragColor = texture2D(texture[7], vUV);
				}
				
				gl_FragColor = gl_FragColor * vec4(totLight, 1.0) + gl_FragColor * emissiveLightIntensity * vec4(1.0, 1.0, 1.0, 1.0);
				
				if (gl_FragColor.r > 1.0) gl_FragColor.r = 1.0;
				if (gl_FragColor.g > 1.0) gl_FragColor.g = 1.0;
				if (gl_FragColor.b > 1.0) gl_FragColor.b = 1.0;
			}
		</script>
		
		<script type="x-shader/x-vertex" id="particlesVertexShader">
		
			attribute vec3 direction;
			attribute vec3 rotation;
			attribute float speed;
			
			uniform float uStartDistance;
			uniform float uEndDistance;
			uniform float uTime;
			uniform float uParticleSize;
			
			varying vec3 vColor;
			
			void main() {
				
				// scale particles
				vec3 nposition = uParticleSize * position;
				
				// covered distance
				float movement = mod((speed * uTime), (uEndDistance - uStartDistance));
				movement = movement + uStartDistance;
				
				// rotate particles
				vec3 angles = radians(100.0 * uTime * rotation );
				vec3 c = cos( angles );
				vec3 s = sin( angles );
				
				mat4 rx = mat4( 1.0,  0.0,  0.0, 0.0,
						0.0,  c.x,  s.x, 0.0,
						0.0, -s.x,  c.x, 0.0,
						0.0,  0.0,  0.0, 1.0 );

				mat4 ry = mat4( c.y, 0.0, -s.y, 0.0,
						0.0, 1.0,  0.0, 0.0,
						s.y, 0.0,  c.y, 0.0,
						0.0, 0.0,  0.0, 1.0 );

				mat4 rz = mat4( c.z, s.z, 0.0, 0.0,
						-s.z,  c.z, 0.0, 0.0,
						0.0,  0.0, 1.0, 0.0,
						0.0,  0.0, 0.0, 1.0 );
				
				vec4 npos = rz * ry * rx * vec4(nposition, 1.0);
				
				// translate particles at the correct position (between start_position and end position)
				// translate WRT particle local position
				vec4 translation = vec4(movement * direction, 0.0);
				npos = npos + translation;
				
				vColor = color;
				gl_Position = projectionMatrix * modelViewMatrix * npos;
			}
		</script>
		<script type="x-shader/x-fragment" id="particlesFragmentShader">
			varying vec3 vColor;
			void main() 
			{
				gl_FragColor = vec4(vColor, 1.0);
			}
		</script>
		
		<script type="x-shader/x-vertex" id="meteoritesVertexShader">
			
			attribute vec2 uvs;			// texture uv mapping
			attribute float yangle;		// starting postion y angle 
			attribute vec3 rotation;	// rotation speed
			attribute vec2 direction;	// direction inside the cloud
			attribute vec2 offset;		// position inside the cloud (computed together with direction)
			attribute float scale;		// particle scale	
			attribute float speed;		// particle angular speed
			
			uniform float u_time;
			uniform float u_cloud_radius;
			uniform float u_section_ratio;
			uniform float u_section_radius;
			
			varying vec3 pos;
			varying vec3 posCameraCoord;
			varying vec3 n;
			varying vec2 vUV;
			
			void main() {
			
				// rotate particles
				vec3 angles = radians(10.0 * u_time * rotation );
				vec3 c = cos( angles );
				vec3 s = sin( angles );
				
				mat4 rx = mat4( 1.0,  0.0,  0.0, 0.0,
						0.0,  c.x,  s.x, 0.0,
						0.0, -s.x,  c.x, 0.0,
						0.0,  0.0,  0.0, 1.0 );

				mat4 ry = mat4( c.y, 0.0, -s.y, 0.0,
						0.0, 1.0,  0.0, 0.0,
						s.y, 0.0,  c.y, 0.0,
						0.0, 0.0,  0.0, 1.0 );

				mat4 rz = mat4( c.z, s.z, 0.0, 0.0,
						-s.z,  c.z, 0.0, 0.0,
						0.0,  0.0, 1.0, 0.0,
						0.0,  0.0, 0.0, 1.0 );
				
				// translate particle
				vec2 in_section_offset = normalize(direction) * u_section_radius;
				in_section_offset.xy *= offset;
				in_section_offset.y *= u_section_ratio;
				
				vec4 translation = vec4(u_cloud_radius, 0.0, 0.0, 0.0);
				translation.xy += in_section_offset;
				//translation.x += u_section_radius * offset.x;
				//translation.y += u_section_radius * offset.y;
				
				vec3 in_cloud_rotation = vec3(0.0, yangle + speed * u_time, 0.0);
				c = cos(in_cloud_rotation);
				s = sin(in_cloud_rotation);
				
				mat4 in_cloud_ry = mat4( c.y, 0.0, -s.y, 0.0,
						0.0, 1.0,  0.0, 0.0,
						s.y, 0.0,  c.y, 0.0,
						0.0, 0.0,  0.0, 1.0 );
				
				translation = in_cloud_ry * translation;
				
				
				mat4 tr = mat4( 1.0, 0.0, 0.0, 0.0,
						0.0,  1.0, 0.0, 0.0,
						0.0,  0.0, 1.0, 0.0,
						translation.x, translation.y, translation.z, 1.0 );
				
				
				//scale particle
				mat4 sc = mat4( scale, 0.0, 0.0, 0.0,
						0.0,  scale, 0.0, 0.0,
						0.0,  0.0, scale, 0.0,
						0.0,  0.0, 0.0, 1.0 );
						
				mat4 modelMtx = tr * sc * ry * rx;
				mat3 normalMtx = mat3(viewMatrix * modelMtx);
				
				vUV = uvs;
				n = (normalMtx * normal).xyz;
				pos = (modelMtx * vec4(position, 1.0)).xyz;
				posCameraCoord = (viewMatrix * modelMtx * vec4(position, 1.0)).xyz;
				
				gl_Position = projectionMatrix * viewMatrix * modelMtx * vec4(position, 1.0);
			}
		</script>
		<script type="x-shader/x-fragment" id="meteoritesFragmentShader">
			struct PointLight {
				vec3 color;
				vec3 position;
				float distance;
			};
		
			uniform float emissiveLightIntensity;
			uniform PointLight pointLights[NUM_POINT_LIGHTS];
			uniform float pointLightIntensity[NUM_POINT_LIGHTS];
			
			uniform sampler2D u_texture;
		
			varying vec3 pos;
			varying vec3 posCameraCoord;
			varying vec3 n;
			
			varying vec2 vUV;
			
			void main()
			{
				// Iterate through lights:
				vec3 totLight = vec3(0.0, 0.0, 0.0);
				for (int i = 0; i < NUM_POINT_LIGHTS; ++i) {
					vec3 l = posCameraCoord - pointLights[i].position;
					if (pointLights[i].distance == 0.0 || length(l) < pointLights[i].distance) {
						vec3 lightDirection = normalize(l);
						totLight += clamp(dot(-lightDirection, n), 0.0, 1.0) * pointLights[i].color * pointLightIntensity[i];
					}
				}
				
				gl_FragColor = texture2D(u_texture, vUV);
				gl_FragColor = gl_FragColor * vec4(totLight, 1.0) + gl_FragColor * emissiveLightIntensity * vec4(1.0, 1.0, 1.0, 1.0);
				
				if (gl_FragColor.r > 1.0) gl_FragColor.r = 1.0;
				if (gl_FragColor.g > 1.0) gl_FragColor.g = 1.0;
				if (gl_FragColor.b > 1.0) gl_FragColor.b = 1.0;

			}
		</script>
		
		<script src="src/Chunk.js"></script>
		<script src="src/NoiseHeightGenerator.js"></script>
		<script src="src/Collisions.js"></script>
		<script src="src/TextureGenerator.js"></script>
		<script src="src/GraphicalTag.js"></script>
		<script src="src/Planet.js"></script>
		<script src="src/ParticlesEffects.js"></script>
		<script src="src/Main.js"></script>
	</head>
	<body>
	</body>
</html>
